task wrapper(type: Wrapper) {
    gradleVersion = '2.1'
}


task installGitHooks(type: Copy) {
	description = "Install git hooks (update version file after commit)"
	
	from 'config/git-hooks'
	into '.git/hooks'
}

def getGitDesc() {
	def cmd = "git describe --match ?.?.?* --dirty"
	
	try {
		def proc = cmd.execute()
		proc.waitFor()					// wait for the command to finish
		return proc.in.text.trim()		// "out" from the process is "in" for gradle
	} catch (IOException e) {
		logger.warn("Could not run '$cmd' - using date - {}", e.getLocalizedMessage());
		return new Date().format('yyyy-MM-dd-HH-mm-ss');
	}
}

subprojects {
	apply plugin: 'java'

	group = 'de.fhg.igd.iva'
	version = getGitDesc()

	compileJava.options.encoding = 'ISO-8859-15'
	javadoc.options.encoding = 'ISO-8859-15'

	sourceCompatibility=1.7

	// We use both Maven Central and the Humboldt Artifactory instance, which contains igd-iva/pcolor
	repositories {
		mavenCentral()
		maven {
			url "http://artifactory.esdi-humboldt.eu/artifactory/libs-release/"
		}
	}

	task sourceJar(type: Jar) {
		description = "Create a JAR with all sources"
		from sourceSets.main.allSource
		classifier = 'sources'
	}

	task javadocJar(type: Jar, dependsOn: javadoc) {
		description = "Create a JAR with the JavaDoc for the java sources"
		from javadoc.destinationDir
		classifier = 'javadoc'
	}

}
