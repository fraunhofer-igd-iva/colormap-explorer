apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "main.ColorMapExplorer"

dependencies {
	compile group: 'org.jbibtex', name: 'jbibtex', version: '1.0.13'
	
	compile project(':metrics')
	compile project(':colormaps')
}

task createVersionFile(type:Copy) {
	description = 'Creates a java version file based on the template in the resources folder'
	def version = getGitDesc()
	
	inputs.property('version', version)	// trigger executing by setting a property

	from('src/main/resources/GitVersion.template')
	into('src/main/java/version')
	rename '(.*).template', '$1.java'

	expand(GIT_VERSION: version)
	
	doLast {
		logger.lifecycle("Updated $outputs.files with $version");
	}
}

compileJava.dependsOn createVersionFile
eclipseProject.dependsOn createVersionFile
