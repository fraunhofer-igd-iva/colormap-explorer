apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'application'

mainClassName = "de.fhg.igd.iva.explorer.main.ColorMapExplorer"

repositories {
	maven {
		url "http://maven.jzy3d.org/releases"
	}
}

configurations {
	natives
}

dependencies {
	compile group: 'org.jbibtex', name: 'jbibtex', version: '1.0.13'
	
	compile project(':metrics')
	compile project(':colormaps')
	
	compile fileTree(dir: 'natives', include: '*.jar')
	
	natives group: 'org.jzy3d', name: 'jzy3d-api', version: '0.9.1'	
}

// A rather ugly workaround until
// https://issues.gradle.org/browse/GRADLE-2553
// is resolved
task copyNatives(type: Sync) {
	from { configurations.natives }
	into { "${projectDir}/natives" }
	
	doLast {
		logger.lifecycle("Copied natives into ${projectDir}/natives")
	}
}

compileJava.dependsOn copyNatives
eclipseClasspath.dependsOn copyNatives

task createVersionFile(type:Copy) {
	description = 'Creates a java version file based on the template in the resources folder'
	def version = getGitDesc()
	
	inputs.property('version', version)	// trigger executing by setting a property

	from('src/main/resources/GitVersion.template')
	into('src/main/java/version')
	rename '(.*).template', '$1.java'

	expand(GIT_VERSION: version)
	
	doLast {
		logger.lifecycle("Updated $outputs.files with $version");
	}
}

compileJava.dependsOn createVersionFile
eclipseProject.dependsOn createVersionFile
